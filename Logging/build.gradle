version = "0.9.0-SNAPSHOT"
group = "com.microsoft.azure"

// region Publishing tasks

uploadArchives {
    if (this.hasProperty("mavenUserPassword")) {
        repositories {
            mavenDeployer {
                configuration = configurations.deployerJars

                repository(url: mavenRepositoryUrl) {
                    authentication(userName: mavenUsername, password: mavenUserPassword)
                }

                // Updating artifacts IDs and dependencies
                addFilter('LogbackJar') { artifact, file ->
                    artifact.name == "LogbackJar"
                }
                pom('LogbackJar') {
                    artifactId = 'applicationinsights-logging-logback'
                    whenConfigured { p -> p.dependencies = p.dependencies.findAll {
                        dep -> dep.artifactId == 'logback-classic' || dep.artifactId == 'logback-core'
                    }.toList() }

                    project {
                        name = "Microsoft Application Insights Logback Appender"
                        description = "This module provides an Application Insights appender implementation for Logback framework"
                    }
                }
                updatePomWithProjectInformation(pom('LogbackJar'))

                addFilter('Log4j1_2Jar') { artifact, file ->
                    artifact.name == "Log4j1_2Jar"
                }
                pom('Log4j1_2Jar') {
                    artifactId = 'applicationinsights-logging-log4j1_2'
                    whenConfigured { p -> p.dependencies = p.dependencies.findAll {
                        dep -> dep.groupId == 'log4j'
                    }.toList() }

                    project {
                        name = "Microsoft Application Insights Log4j 1.2 Appender"
                        description = "This module provides an Application Insights appender implementation for Log4j 1.2 framework"
                    }
                }
                updatePomWithProjectInformation(pom('Log4j1_2Jar'))

                addFilter('Log4j2Jar') { artifact, file ->
                    artifact.name == "Log4j2Jar"
                }
                pom('Log4j2Jar') {
                    artifactId = 'applicationinsights-logging-log4j2'
                    whenConfigured { p -> p.dependencies = p.dependencies.findAll {
                        dep -> dep.artifactId == 'log4j-core' || dep.artifactId == 'log4j-api'
                    }.toList() }

                    project {
                        name = "Microsoft Application Insights Log4j 2 Appender"
                        description = "This module provides an Application Insights appender implementation for Log4j 2 framework"
                    }
                }
                updatePomWithProjectInformation(pom('Log4j2Jar'))
            }
        }
    }
}

// endregion Publishing tasks

dependencies {
    compile project(':core')
    compile group: 'commons-io', name: 'commons-io', version: '2.4'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.1'
    compile group: 'log4j', name: 'log4j', version: '1.2.17'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.9'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.2'
    compile group: 'ch.qos.logback', name: 'logback-core', version: '1.1.2'
    testCompile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.1'
}

// region Create JAR tasks

task createLog4j1_2Jar(type: Jar) {
    from(sourceSets.main.output) {
        include "com/microsoft/applicationinsights/log4j/v1_2/**", "com/microsoft/applicationinsights/common/**"
        baseName = 'Log4j1.2'
        archiveName = 'appinsights-appender-' + baseName + '-' + version + '.jar'
    }
}

task createLog4j2Jar(type: Jar) {
    from(sourceSets.main.output) {
        include "com/microsoft/applicationinsights/log4j/v2/**", "com/microsoft/applicationinsights/common/**"
        baseName = "Log4j2"
        archiveName = 'appinsights-appender-' + baseName + '-' + version + '.jar'
    }
}

task createLogbackJar(type: Jar) {
    from(sourceSets.main.output) {
        include "com/microsoft/applicationinsights/logback/**", "com/microsoft/applicationinsights/common/**"
        baseName = 'Logback'
        archiveName = 'appinsights-appender-' + baseName + '-' + version + '.jar'
    }
}

task buildJars << {
    createLog4j1_2Jar.execute()
    createLog4j2Jar.execute()
    createLogbackJar.execute()
}

tasks.uploadArchives.dependsOn(buildJars)
tasks.buildJars.dependsOn(createLog4j1_2Jar)
tasks.buildJars.dependsOn(createLog4j2Jar)
tasks.buildJars.dependsOn(createLogbackJar)

// endregion Create JAR tasks

// region Create artifacts

artifacts {
    archives(createLog4j1_2Jar) {
        name = 'Log4j1_2Jar'
    }

    archives(createLog4j2Jar) {
        name = 'Log4j2Jar'
    }

    archives(createLogbackJar) {
        name = 'LogbackJar'
    }
}

// endregion Create artifacts