/*
 * ApplicationInsights-Java
 * Copyright (c) Microsoft Corporation
 * All rights reserved.
 *
 * MIT License
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this
 * software and associated documentation files (the ""Software""), to deal in the Software
 * without restriction, including without limitation the rights to use, copy, modify, merge,
 * publish, distribute, sublicense, and/or sell copies of the Software, and to permit
 * persons to whom the Software is furnished to do so, subject to the following conditions:
 * The above copyright notice and this permission notice shall be included in all copies or
 * substantial portions of the Software.
 * THE SOFTWARE IS PROVIDED *AS IS*, WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
 * INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR
 * PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE
 * FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR
 * OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
 * DEALINGS IN THE SOFTWARE.
 */

apply from: "$buildScriptsDir/common-java.gradle"

def sdkVersionFileDir = "$project.buildDir/src/generated/main/resources"

sourceSets {
    main {
        resources {
            srcDir sdkVersionFileDir
        }
    }
}

import com.microsoft.applicationinsights.build.tasks.PropsFileGen

archivesBaseName = 'applicationinsights-core'

configurations {
    forTheDLL { transitive = false }
}

dependencies {
    compile project(":agent:agent-profiler:agent-profiler-api")
    compile project(":agent:agent-profiler:agent-alerting")
    compile group: 'commons-io', name: 'commons-io', version: versions.commonsIo
    compile group: 'org.apache.commons', name: 'commons-lang3', version: versions.commonsLang
    compile group: 'org.apache.commons', name: 'commons-text', version: versions.commonsText
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: versions.apacheHttpClient
    compile group: 'com.squareup.moshi', name: 'moshi', version: versions.moshi
    compile group: 'com.google.code.gson', name: 'gson', version: versions.gson
    // update transitive dependency version
    compile group: 'com.google.guava', name: 'guava', version: versions.guava
    compile group: 'com.github.oshi', name:'oshi-core', version: versions.oshi
    compile group: 'org.slf4j', name: 'slf4j-api', version: versions.slf4j

    testCompile group: 'junit', name: 'junit', version: versions.junit
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: versions.hamcrest
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: versions.hamcrest
    testCompile group: 'org.mockito', name: 'mockito-core', version: versions.mockito
    testCompile group: 'com.github.stefanbirkner', name: 'system-rules', version: versions.systemRules
    testCompile group: 'com.google.code.gson', name: 'gson', version: versions.gson
    testCompile group: 'io.github.hakky54', name: 'logcaptor', version: versions.logcaptor

    testCompile project(":agent:agent-profiler:agent-service-profiler")

    forTheDLL 'com.microsoft.azure:applicationinsights-core:2.6.2'
}

task copyTheDLL(type: Copy) {
    from zipTree(configurations.forTheDLL.singleFile).matching {
        include '*.dll'
    }
    into "$buildDir/resources/main"
}

processResources.dependsOn copyTheDLL

task generateVersionProperties(type: PropsFileGen) {
    targetFile = new File(sdkVersionFileDir, "sdk-version.properties")
    property "version", project.version
}

processResources.dependsOn generateVersionProperties